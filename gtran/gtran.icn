#  gtran.icn -- (generic) driver for Goaldi translator.

invocable all

procedure cat(getline, args)
    local fname, f, p

    every fname := !args do {
        f := open(fname) | stop("cannot open ", fname)
        suspend "#line 0 " || image(fname)
        suspend !f
    }
    if *args = 0 then {
        if /getline then {
            suspend !&input
        } else {
            while p := @getline do {
                suspend p
            }
        }
    }
end

procedure tee(c, L)
    local fn, f, v
    fn := get(L) | stop("ERROR: tee: no filename")
    f := open(fn, "w") | stop("ERROR: cannot write ", fn)
    while v := @c do {
	write(f, dump_verbose(v, "", ""))
	suspend v
    }
end

procedure stdout(c)
    local p
    while p := @\c do {
        write(p)
    }
end

procedure devnull(c)
    while @\c
end

procedure main(L) 
    local args, k, fn, c

    while L[1] do {
        args := []
        while k := pop(L) & k ~== ":" do {
            put(args, k)
        }
        fn := pop(args)
        fn := proc(fn) | stop("ERROR: unknown command-line procedure, ", fn)
        c := create fn(c, args)
    }
    while @c
end
