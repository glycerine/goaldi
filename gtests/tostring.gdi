#  Image and string conversion examples for every Goaldi type

record Circle(x, y, r)

procedure main()
	local a, m, v, fmt

	m := map()
	m[1] := "a"
	m[2] := "b"

	# make a collection, and sort it
	a := [, 1, &pi, 6.02214129e23, "abc", &stdin, [].push, main,
		channel(3), Circle, Circle(3,4,5), [1,2,3], m, !m.sort()]
	a := a.sort()

	# print header
	fmt := "%-12s %-12s %-24s %s"
	printf(fmt, "type", "string", "image", "write\n")
	printf(fmt, "----", "------", "-----", "-----\n")

	# print conversion examples, ending with implicit conversion by write()
	every v := !a do {
		printf(fmt,
			v.type()   | "[failed]",
			v.string() | "[failed]",
			v.image()  | "[failed]", "") 
		write(&stdout, v)
	}
end
