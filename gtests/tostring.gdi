#SRC: goaldi original
#
#  Image and string conversion examples for every Goaldi type
#  Results are printed in sort() order
#
#  The result of string(v) is the same as used by write(v).
#  The result of image(v) is the same as used in error traceback.

record Circle(x, y, r)

procedure main(args) {
	# local a, m, v, fmt
	local a
	local m
	local v
	local fmt

	m := table()
	m[1] := "a"
	m[2] := "b"

	# make a collection, and sort it
	a := [, 1, %pi, 6.02214129e23, "abc", %stdin, [].push, main, write,
		channel(3), Circle, Circle(3,4,5), [1,2,3], m, !m.sort()]
	a := a.sort()

	# print header
	fmt := "%-15s %-15s %s\n"
	printf(fmt, "type(v)", "string(v)", "image(v)")
	printf(fmt, "-------", "---------", "--------")

	# print conversion examples, ending with implicit conversion by write()
	every v := !a do {
		printf(fmt,
			v.type()   | "[failed]",
			v.string() | "[failed]",
			v.image()  | "[failed]") 
	}
}
