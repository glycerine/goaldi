#SRC: Goaldi original
#
#  Show examples of all types

global format

record P(x,y)

procedure P.xy() { return self.x * self.y }

procedure main() {
	format := "%-13s %-13s %-9v %-22s %v\n"
	write()
	printf(format, "global", "type", "string", "image", "external")
	printf(format, "------", "----", "------", "-----", "--------")
	show(, nil)
	show(type, type())
	show(number, 17)
	show(number, %pi)
	show(number, 6.02214129e23)
	show(string, "abcd")
	show(file, %stdin)
	show(channel, channel(3))
	show(constructor, P)
	show(methodvalue, P().xy)
	show(, show)
	show(list, [2,3,5,7,11])
	show(table, table(){"Fe":"Iron","Au":"Gold"})
	show(P, P(7,5))
	show(external, duration(3600+120+3))
	write()
}

procedure show(g, v) {
	local t := type(v)
	printf(format, (t === g) | "", t, string(v), image(v), external(v))
}
