#SRC: Goaldi original
#
#  Show examples of all types

global format

record Circle(x,y,r)

procedure Circle.area() { return %pi * self.r ^ 2 }

procedure main() {
	format := "%-10s %-13s %-9v %-27s %v\n"
	write()
	printf(format, "global", "type", "string", "image", "external")
	printf(format, "------", "----", "------", "-----", "--------")
	show(, nil)
	show(type, type())
	show(number, 17)
	show(number, %pi)
	show(number, 6.02214129e23)
	show(string, "abcd")
	show(file, %stdin)
	show(channel, channel(3))
	show(, Circle)
	show(, Circle().area)
	show(, show)
	show(list, [2,3,5,7,11])
	show(table, table(){"Fe":"Iron","Au":"Gold"})
	show(Circle, Circle(7,5,2))
	show(external, duration(3600+120+3))
	write()
}

procedure show(ctor, value) {
	local t := type(value)
	printf(format, (t === ctor) | "", t,
		string(value), image(value), external(value))
}
